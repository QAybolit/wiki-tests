plugins {
    id 'java'
    id 'io.qameta.allure' version '2.12.0'
}

def junitVersion = "5.11.3",
    allureVersion = "2.24.0",
    aspectjVersion = "1.9.20.1",
    slf4jVersion = "2.0.7",
    selenideVersion = "7.5.1",
    ownerVersion = "1.0.12"

repositories {
    mavenCentral()
}

allure {
    report {  // allure-commandline
        version.set(allureVersion)
    }
    adapter { // отвечает за появление папочки build/allure-results
        aspectjWeaver.set(true) //обработка аннотации @Step
        frameworks {
            junit5 { //название фреймворка
                adapterVersion.set(allureVersion) //версия интеграции фреймворка и Allure
            }
        }
    }
}

dependencies {
    testImplementation(
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "io.qameta.allure:allure-junit5:$allureVersion",
            "org.aspectj:aspectjweaver:$aspectjVersion",
            "org.slf4j:slf4j-simple:$slf4jVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "com.codeborne:selenide:$selenideVersion",
            "org.aeonbits.owner:owner:$ownerVersion"
    )
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    systemProperties(System.getProperties())

    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat "full"
    }
}

tasks.register("local-test", Test) {

    useJUnitPlatform {
        includeTags("local")
    }
}

tasks.register("remote-test", Test) {

    useJUnitPlatform {
        includeTags("remote")
    }
}